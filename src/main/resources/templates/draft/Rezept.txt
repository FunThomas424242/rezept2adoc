Rezeptdatei:
rezept=Dateiart
;

Dateiart:
projekt = RezeptbuchProjekt | rezepte = Rezeptliste | produkte= Produktliste;


RezeptbuchProjekt:
'RezeptSammlung' '{'
'groupId' '=' groupId=STRING ';'
'artifactId' '=' artifactId=STRING ';'
'version' '=' version=STRING ';'
'Media Folder' ':' resourcesDir=STRING ';'
('siteURL' '=' siteURL=STRING ';')?
'}' buch=BuchBeschreibung
;

BuchBeschreibung:
'Buch' '{'
(imports+=RezeptImport)*
'Titel:' titel=STRING ';'
(autoren+=Autor)+
('Vorwort:' vorwort=STRING 	(absatz+=Absatz)* )?
(lizenz=Lizenz)?
('Veröffentlicht am:' datumPublished=DATUM '.')?
'}'
;

Absatz:
'Absatz:'
(titel=HEADER)?
text=STRING
;

Autor:
'Autor:' '{'
'Vorname:' vorname=STRING
'Nachname:' nachname=STRING
('Organisation:' organisationsName=STRING)?
'ModifikationsNotiz:' modifikationsNotiz=STRING
'}'
;

Lizenz:
'Lizenzhinweis:' hinweis=STRING
('Lizenztext:' text=STRING)?
;

RezeptImport:
'import rezepte' importURI = STRING ';'
;


Produktliste:
'Produktliste' name=STRING ';'
(rezepte+=ProduktBlatt)+
;

ProduktBlatt:
{ProduktBlatt}
'Produkt' name=STRING 	'{'
('Verpackung:' verpackung=Verpackung '.')?
menge=BestimmteMenge (', SmartPoints:' smartPoints=INT)? '.'
('EAN:' ean=STRING '.')?
('UBA:' uba=STRING '.')?
preis=Preis?
('Händler:' haendler=STRING '.')?
('Hersteller:' hersteller=STRING '.')?
'Letzte Änderung:' datumLetzteAenderung=DATUM '.'
'}'
;

Preis:
'Preis:' betrag=Float waehrung=STRING '.'
;

Rezeptliste:
'Rezeptliste' name=STRING ';'
(zitat=Zitat)?
(bild=Bild)?
(zusammenfassung=Abstract)?
(imports+=ProduktImport)*
(rezepte+=RezeptBlatt)+
;

ProduktImport:
'import produkte' importURI = STRING 'alias' produkte=[Produktliste|STRING] ';'
;

RezeptBlatt:
{RezeptBlatt}
'Rezept' name=STRING '{'
'Titel:' titel=STRING '.'
('Untertitel:' untertitel=STRING '.')?
'Kategorie:' kategorie=STRING '.'
('Quelle:' quelle=Quelle)?
('Ausreichend für' personen=INT 'Personen.')?
'Letzte Änderung:' letzteAenderung=DATUM '.'
(tags+=Tag)+
(produkte+=ProduktRef)*
(hasBrennwert="Brennwert pro Portion" brennwert=INT "WSP.")?
(zutaten+=Zutat)+
(schritte+=Arbeitsschritt)+
('--Zusatzinformationen--'
(notizen+=Notiz)*
(bilder+=Bild)*
)?
'}';

Arbeitsschritt:
'Aktion:' beschreibung=STRING
(notiz=Notiz)?
;

Notiz:
Tipp | Kommentar | Anmerkung
;

Tipp:
'Tipp:' '{'
text=STRING
'}'
;

Kommentar:
'Kommentar:' '{'
text=STRING
'}'
;

Anmerkung:
'Anmerkung:' '{'
text=STRING
'}'
;

Abstract:
'Zusammenfassung:' '{'
text=STRING
'}'
;

Zitat:
'Zitat:' '{'
'Verfasser:' verfasser=STRING
'Text:' text=STRING
'}'
;

Bild:
'Bild:' '{'
ablageort=Ablageort
('InternetPage' internetPage=Internetpfad)?
('Text:' beschreibung=STRING)?
('Lizenz:' lizenz=STRING)
('Urheber:' urheber=STRING)?
'}'
;

Ablageort:
Internetpfad | Lokalerpfad
;

Internetpfad:
'URL:' url=STRING
;

Lokalerpfad:
'FileName:' fileName=STRING
;

ProduktRef:
'ProduktRef:'
produkt=[ProduktBlatt|STRING]
'Menge:' anzahl=Float 'mal.'
;

Zutat:
'Zutat:' name=STRING menge=Menge ( hasSmartPoints=', SmartPoints:' smartPoints=INT )? '.'
;

Menge:
BestimmteMenge | UnbestimmteMenge
;

BestimmteMenge:
'Menge:' betrag=Float (details=STRING? einheit=Masseinheit)?
;

UnbestimmteMenge:
menge=UnbestimmteMengenangabe
;

Quelle:
Literaturquelle | Personenquelle
;

Literaturquelle:
'übernommen' modifikation=Modifikationsart (('aus' beschreibung=STRING)|('von' url=STRING))  '.'
;


Personenquelle:
'überliefert' 'von' personenBeschreibung=STRING '.'
;

Tag:
DiaetTag | StoffTag | BenutzerTag
;

StoffTag:
'Stoff:' stoff=STRING
;

BenutzerTag:
'Tag:' bezeichnung=STRING
;

DiaetTag:
'Diät:' diaet=STRING
;


Float :
STRING
;

enum Modifikationsart:
MODIFIZIERT='verändert' | UNVERAENDERT='unverändert'
;

enum Masseinheit:
KEINEAUSWAHL='keine Auswahl'
| KG='kg'
| G='g'
| ML='ml'
| L='L'
| PRISE='Prise'
| STUECK='Stück'
| STANGE='Stange(n)'
| ZEHEN='Zehe(n)'
| KNOLLE='Knolle(n)'
| SCHEIBEN='Scheibe(n)'
| BUND='Bund'
| EL='Esslöffel'
| TL='Teelöffel'
| MESSERSPITZE='Messerspitze(n)'
| TASSE= 'Tasse(n)'
| DOSE='Dose(n)'
| BECHER='Becher'
| TUETEN='Tüte(n)'
| PACKUNG='Packung(en)'
| PCKCH='Päckchen'
;

enum UnbestimmteMengenangabe:
KEINEAUSWAHL='keine Auswahl'
| NACHBEDARF='nach Bedarf'
| ETWAS='etwas'
| WENIG='wenig'
;

enum Verpackung:
KEINEAUSWAHL='""'
| GLAS='"Glas"'
| DOSE='"Dose"'
| TUETE='"Tüte"'
| NETZ='"Netz"'
| PCKG='"Packung"'
| PCKCH='"Päckchen"'
| BECHER='"Becher"'
| BEUTEL='"Beutel"'
| STUECK='"Stück"'
;

//terminal BEZEICHNER : ('a'..'z'|'A'..'Z'|'ä'..'ü'| 'Ä'..'Ü'|'#'|'_') ('a'..'z'|'A'..'Z'|'ä'..'ü'| 'Ä'..'Ü'|'#'|'_'|'0'..'9')*;
terminal DATUM : ('0'..'9') ('0'..'9') '.' ('0'..'9') ('0'..'9') '.' ('0'..'9') ('0'..'9')('0'..'9') ('0'..'9');
terminal HEADER :
'==' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'=')  )+ '==' |
'--' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'-')  )+ '--'
;